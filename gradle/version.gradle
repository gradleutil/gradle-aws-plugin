// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath 'org.ajoberstar:grgit:1.7.0'
	}
}

if (project.version?.equals('unspecified')) {
	def getGitVersion = {
		def describedCommit
		try{
			def git = org.ajoberstar.grgit.Grgit.open(file('.'))
			describedCommit = git.describe().toString().trim()
			describedCommit += describedCommit.matches(".*-[0-9]+-g[0-9a-f]{7}") ? "-SNAPSHOT" : ""
			describedCommit += git.status().isClean() ? "" : "+dirty"
		} catch (Exception e){
			logger.error('Could not get git version:' + e.message)
			describedCommit = file('.version').text + '-SNAPSHOT'
		}
		describedCommit ?: file('.version').text
	}
	project.version = getGitVersion()
}

logger.lifecycle("version ${version}")

task showVersion {
	doLast {
		println version
	}
}
