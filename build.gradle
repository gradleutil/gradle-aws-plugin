// -*- coding: utf-8; mode: groovy -*-

buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "com.diffplug.spotless:spotless-plugin-gradle:5.9.0"
		classpath "de.thetaphi:forbiddenapis:3.1"
		classpath "de.aaschmid:gradle-cpd-plugin:3.1"
		classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.14"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.6.0"
        classpath "io.freefair.gradle:lombok-plugin:5.3.0"
	}
}

group = "net.gradleutil"
ext.artifactId = "gradle-aws-plugin"

apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "io.freefair.lombok"
apply plugin: "com.gradle.plugin-publish"

// gradle configuration
apply from: 'gradle/version.gradle'
apply from: 'gradle/resolveDependencies.gradle'

defaultTasks "clean", "build"

// ======== code quality ========
apply plugin: "checkstyle"
apply plugin: "com.github.spotbugs"
apply plugin: "pmd"
apply plugin: "cpd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "com.diffplug.spotless"

// compiler
sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(AbstractCompile) each {
	it.options.encoding = "UTF-8"
}

compileJava {
options.compilerArgs << "-Werror"
	options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-deprecation"
}

generateLombokConfig.enabled = false

// jacoco
jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
	}
}

// checkstyle
checkstyle {
	toolVersion = "7.1.2"
	showViolations = true
	configFile = project.file('config/checkstyle/checkstyle.xml')
}
checkstyleTest {
	configFile = file("config/checkstyle/checkstyle-test.xml")
}

// workaround: see https://issues.gradle.org/browse/GRADLE-2888
task verifyNoCheckstyleWarningsInMain {
	group = "verification"
	description = "Fail if checkstyleMain result contains warnings"
	doLast {
		File warningsFile = file('build/reports/checkstyle/main.xml')
		if (warningsFile.exists() && warningsFile.text.contains("<error ")) {
			throw new GradleException("There were checkstyle warnings! For more info check $warningsFile")
		}
	}
}
checkstyleMain.finalizedBy verifyNoCheckstyleWarningsInMain
task verifyNoCheckstyleWarningsInTest {
	group = "verification"
	description = "Fail if checkstyleTest result contains warnings"
	doLast {
		File warningsFile = file('build/reports/checkstyle/test.xml')
		if (warningsFile.exists() && warningsFile.text.contains("<error ")) {
			throw new GradleException("There were checkstyle warnings! For more info check $warningsFile")
		}
	}
}
checkstyleTest.finalizedBy verifyNoCheckstyleWarningsInTest

// spotbugs
spotbugs {
	ignoreFailures = false
	showStackTraces = true
	showProgress = true
	effort = 'default'
	reportsDir = file("$buildDir/spotbugs")
	includeFilter = file("config/findbugs/excludeFilter.xml")
	excludeFilter = file("config/findbugs/excludeFilter.xml")
}


// pmd
pmd {
	consoleOutput = true
	toolVersion = '6.30.0'
}
pmdMain {
	ruleSetFiles = files("config/pmd/pmd-settings.xml")
	ruleSets = []  // To apply only the custom rules
}
pmdTest {
	ruleSetFiles = files("config/pmd/pmd-settings-test.xml")
	ruleSets = []  // To apply only the custom rules
}
cpdCheck {
	reports {
		text.enabled = true
		xml.enabled = false
	}
	source = sourceSets.main.allJava // only main source
	ignoreFailures = true
}

// forbiddenapis
forbiddenApisMain {
	bundledSignatures += [
		'jdk-system-out',
		'jdk-unsafe-' + project.sourceCompatibility,
		'jdk-internal-' + project.sourceCompatibility
	]
	signaturesFiles = fileTree(dir: "config/forbiddenapis", include: "*.txt")
	ignoreFailures = false
}
forbiddenApisTest {
	bundledSignatures += [
		'jdk-system-out',
		'jdk-internal-' + project.sourceCompatibility
	]
	signaturesFiles = fileTree(dir: "config/forbiddenapis-test", include: "*.txt")
	ignoreFailures = false
}

// spotless
spotless {
	java {
		licenseHeaderFile 'config/spotless/spotless.license.java'
		importOrderFile   'config/spotless/spotless.importorder'
		eclipse().configFile 'config/spotless/spotless.eclipseformat.xml'

		// Eclipse formatter screws up long literals with underscores inside of annotations (see issue #14)
		//    @Max(value = 9_999_999 L) // what Eclipse does
		//    @Max(value = 9_999_999L)  // what I wish Eclipse did
		custom 'Long literal fix', { it.replaceAll('([0-9_]+) [Ll]', '$1L') }

		// Eclipse formatter puts excess whitespace after lambda blocks
		//    funcThatTakesLambdas(x -> {} , y -> {} )	// what Eclipse does
		//    funcThatTakesLambdas(x -> {}, y -> {})	// what I wish Eclipse did
		custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }

		indentWithTabs()
		endWithNewline()

		replaceRegex 'Add space before comment asterisk', '^(\\t*)\\*', '$1 *'
//		customReplaceRegex 'Remove indent before line comment', '^\\t*//', '//'
	}
}

// ======== create source and javadoc bundles ========

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
}

javadoc {
	failOnError = false
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

configurations {
	deployerJars
}

// ======== libraries ========
repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	implementation gradleApi()
	implementation "com.google.guava:guava:$guavaVersion"
	implementation "commons-io:commons-io:1.4"

	implementation "com.amazonaws:aws-java-sdk-sts:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-s3:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-ec2:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-rds:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-route53:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-elasticloadbalancing:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-elasticbeanstalk:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-cloudformation:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-lambda:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-iam:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-sqs:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-sns:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-ecr:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-ssm:$awsJavaSdkVersion"
	implementation "com.amazonaws:aws-java-sdk-cloudwatch:$awsJavaSdkVersion"

	// tests
	testImplementation "junit:junit:$junitVersion"
	testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
	testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"

	deployerJars "org.springframework.build:aws-maven:4.7.0.RELEASE"
}

// ======== deploy artifacts ========
// Allows forks of the plugin to define their own deployment mechanisms
// in separate files named according to their maven group name
apply from: "deploy/${group}.gradle"
